group 'com.example'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.2.41'
    ext.dropwizard_version = '1.3.2'
    ext.config_file ='config.yml'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-noarg:$kotlin_version"
        classpath 'eu.appsatori:gradle-fatjar-plugin:0.2'
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'fatjar'
apply plugin: 'application'
apply plugin: 'kotlin-jpa'

sourceCompatibility = 1.8
targetCompatibility = 1.8

mainClassName = 'com.dropwizard.study.config.StudyApplication'

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "com.fasterxml.jackson.module:jackson-module-kotlin:2.9.+"

    compile "io.dropwizard:dropwizard-core:$dropwizard_version"
    compile "io.dropwizard:dropwizard-hibernate:$dropwizard_version"
    compile "io.dropwizard:dropwizard-migrations:$dropwizard_version"

    runtime 'com.h2database:h2:1.3.148'

    // JAXB included for Java 9+
    compile 'javax.xml.bind:jaxb-api:2.2.11'
    compile 'com.sun.xml.bind:jaxb-core:2.2.11'
    compile 'com.sun.xml.bind:jaxb-impl:2.2.11'
    compile 'javax.activation:activation:1.1.1'

    compile group: 'org.postgresql', name: 'postgresql', version: '42.2.5'

    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile "io.dropwizard:dropwizard-testing:$dropwizard_version"
    testCompile "io.dropwizard:dropwizard-client:$dropwizard_version"
    testCompile 'org.mockito:mockito-core:2.8.9'
}

fatJar {
    manifest {
        attributes 'Main-Class': mainClassName
    }

    // Exclude these files to avoid jar signature errors
    exclude 'META-INF/*.RSA', 'META-INF/*.SF','META-INF/*.DSA'
}

// Start the Dropwizard service
run {
    args 'server', config_file
}

// Check the status of the database with Liquibase
task dbStatus(type: JavaExec) {
    main mainClassName
    classpath sourceSets.main.runtimeClasspath
    args 'db', 'status', config_file
}

// Migrate the latest db changes with Liquibase
task dbMigrate(type: JavaExec) {
    main mainClassName
    classpath sourceSets.main.runtimeClasspath
    args 'db', 'migrate', config_file
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}


